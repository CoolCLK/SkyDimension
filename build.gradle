buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: forge_gradle_version, changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: mixins_gradle_version, changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = mod_group
archivesBaseName = mod_id

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: mod_minecraft_mappings_channel, version: mod_minecraft_mappings_versions
    accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_dependency_version}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "META-INF/mods.toml"
        expand "mod_id": mod_id, "mod_name": mod_name, "mod_description": mod_description, "mod_version": mod_version, "mod_minecraft_version_range": mod_minecraft_version_range, "mod_url": mod_url, "mod_update_url": mod_update_url, "mod_authors": mod_authors, "mod_credits": mod_credits, "forge_loader_version_range": forge_loader_version_range
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "META-INF/mods.toml"
    }
}

jar {
    manifest {
        attributes([
            "FMLCorePluginContainsFMLMod": true,
            "FMLCorePlugin": "coolclk.skydimension.forge.injection.MixinFMLLoadingPlugin",
            "ForceLoadAsMod": true,
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "META-INF/accesstransformer.cfg",
        ])
    }
}

mixin {
    add sourceSets.main, 'mixins.skydimension.refmap.json'
    config mixins_config
}