buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: forge_gradle_version, changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: mixins_gradle_version, changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = mod_group
archivesBaseName = mod_id

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: mod_minecraft_mappings_channel, version: mod_minecraft_mappings_versions
    accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_dependency_version}"

    compile("org.spongepowered:mixin:${mixins_dependency_version}") {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    annotationProcessor "org.spongepowered:mixin:${mixins_processor_version}:processor"
}

processResources {
    inputs.property "mod_id", mod_id
    inputs.property "mod_name", mod_name
    inputs.property "mod_description", mod_description
    inputs.property "mod_version", mod_version
    inputs.property "mod_minecraft_version_range", mod_minecraft_version_range
    inputs.property "mod_url", mod_url
    inputs.property "mod_update_url", mod_update_url
    inputs.property "mod_authors", mod_authors
    inputs.property "mod_credits", mod_credits
    inputs.property "forge_loader_version_range", forge_loader_version_range
    filteringCharset "UTF-8"
    
    filesMatching("META-INF/mods.toml") {
        expand "mod_id": mod_id,
                "mod_name": mod_name,
                "mod_description": mod_description,
                "mod_version": mod_version,
                "mod_minecraft_version_range": mod_minecraft_version_range,
                "mod_url": mod_url,
                "mod_update_url": mod_update_url,
                "mod_authors": mod_authors,
                "mod_credits": mod_credits,
                "forge_loader_version_range": forge_loader_version_range
    }
}

jar {
    manifest {
        attributes([
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
            "FMLAT": "META-INF/accesstransformer.cfg",
        ])
    }
}

mixin {
    add sourceSets.main, 'mixins.skydimension.refmap.json'
    config mixins_config
}